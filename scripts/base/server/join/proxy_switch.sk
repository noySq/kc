on custom redis message:

	if redis channel = {-config::redis::channel::connection-listener}:

		encode redis message as json to {_json::*}

		set {_server} to {_json::server::from}
		if {_server} != "velocity":
			stop

		set {_event} to {_json::message::event}
		if {_event} != "SWITCH":
			stop

		set {_hostname} to get_hostname()
		set {_target_server} to {_json::message::server}
		if {_target_server} != {_hostname}:
			stop

		set {_p} to ({_json::message::uuid} parsed as offlineplayer)
		set {_name} to {_json::message::username}

		set {_previous_server} to {_json::message::previous_server}

		set {_channel::old} to join ({-config::redis::channel::online-players}, {_previous_server})
		set {_channel::new} to join ({-config::redis::channel::online-players}, {_hostname})

		remove_from_redis_list({_name}, {_channel::old})
		add_to_redis_list({_name}, {_channel::new})

		unlist all players for all players

		add_all_fake_players({_p})
		add_fake_player({_p})

		set_tablist({_p})
		refresh_scoreboard({_p})

		# TODO
		# Kellene egy jobb megoldás
		# Enélkül, ha teleportálsz a vadonban azonnal visszadob :D
		if get_hostname() != "kockacraft-vadon":
			spawn({_p})