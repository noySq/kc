function set_attributes(item: item, material: text, level: integer) :: item:

	set {_type} to get_item_type({_item})
	
	if {_type} is "unknown":
		stop

	set {_bonuses::*} to keyed get_upgrade_bonuses({_type}, {_material}, {_level})

	if {_bonuses::*} is not set:
		stop
	
	loop indices of {_bonuses::*}:

		set {_attribute::name} to loop-value
		set {_attribute::type} to ({_attribute::name} parsed as attribute type)
		set {_attribute::modifier} to {_attribute::type} modifier of {_item}

		set {_attribute::value::current} to (modifier amount of {_attribute::modifier}) ? 0
		set {_attribute::value::increase} to {_bonuses::%{_attribute::name}%}
		set {_attribute::value::new} to ({_attribute::value::current} + {_attribute::value::increase})

		delete {_attribute::type} attribute modifiers of {_item}

		apply attribute modifier to {_item}:
			attribute: {_attribute::type}
			id: "kockacraft:%{_attribute::name}%"
			slot: mainhand_slot_group
			amount: {_attribute::value::new}
			operation: add_scalar
	
	return {_item}
