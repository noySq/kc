import:
    com.sk89q.worldedit.bukkit.BukkitWorld
    com.sk89q.worldedit.extent.clipboard.Clipboard
    com.sk89q.worldedit.extent.clipboard.io.ClipboardFormats
    com.sk89q.worldedit.math.BlockVector3
    com.sk89q.worldedit.regions.CuboidRegion
    java.io.File
    java.nio.file.Files
    java.nio.file.Paths

on load:
	create new section with arguments variables {_ID} stored in {-section::save_island_schematic}:

		set {_file_location} to {-config::island::save_location}
		set {_file_name} to "%{_ID}%.schem"
		set {_absolute_path} to join {_file_location} and {_file_name}

		Files.deleteIfExists(Paths.get({_absolute_path}))
		set {_file} to new File({_absolute_path})

		set {_vertices::*} to get_opposite_vertices({_ID})
		set {_middle} to get_island_middle({_ID})

		set {_world} to new BukkitWorld({_middle}.getWorld())
		set {_location::1} to BlockVector3.at({_vertices::1}.getBlockX(), {_vertices::1}.getBlockY(), {_vertices::1}.getBlockZ())
		set {_location::2} to BlockVector3.at({_vertices::2}.getBlockX(), {_vertices::2}.getBlockY(), {_vertices::2}.getBlockZ())
		set {_origin} to BlockVector3.at({_middle}.getBlockX(), {_middle}.getBlockY(), {_middle}.getBlockZ())

		set {_region} to new CuboidRegion({_world}, {_location::1}, {_location::2})
		set {_clipboard} to Clipboard.create({_region})
		{_clipboard}.setOrigin({_origin})

		set {_format} to ClipboardFormats.findByAlias("schem")
		{_clipboard}.save({_file}, {_format})

		upload_file({-config::island::minio::bucket}, {_file_name}, {_absolute_path})
		Files.deleteIfExists(Paths.get({_absolute_path}))