#
# Játékos meghívása a sziget tagjai közé
#
#    Args:
#        id (integer): A sziget azonosítója
#        target (Player): A meghívott játékos
#        sender (Player): A meghívót küldő játékos
#
function invite_player(ID: text, target: player, sender: player):

	if get_player_island_id({_target}) is set:
		stop

	if get_island_members({_ID}) contains {_target}:
		stop

	if {-island::%uuid of {_target}%::invite} is set:
		stop

	set {-island::%uuid of {_target}%::invite} to {_ID}

	send "" to {_target}
	send feedback("casual", "%{_sender}% meghívott a(z) %{_ID}% szigetre") to {_target}
	send feedback("casual", "fogadd el a ß/sziget elfogad &fparanccsal") to {_target}
	send "" to {_target}

	send feedback("casual", "%get_displayname({_target})% meg lett hívva a szigetre") to get_island_members({_ID})

	wait 30 seconds

	if {-island::%uuid of {_target}%::invite} is set:

		send feedback("warning", "lejárt a meghívó") to {_target}
		delete {-island::%uuid of {_target}%::invite}