command /npc [<text>] [<text>] [<text>] [<text>]:

	prefix: kockacraft
	executable by: players
	usage: &f/npc
	description: NPC manager
	permission: npc
	trigger:

		if arg-1 = "create":

			if arg-2 is not set:
				send feedback("error", "adj nevet")
				stop

			if get_all_npc() contains arg-2:
				send feedback("error", "már van")
				stop

			create_npc(arg-2)
			send feedback("success", "%arg-2% létrehozva")

		if arg-1 = "delete":

			if arg-2 is not set:
				send feedback("error", "adj nevet")
				stop

			if get_all_npc() does not contain arg-2:
				send feedback("error", "nem létezik")
				stop

			delete_npc(arg-2)
			send feedback("success", "%arg-2% törölve")

		if arg-1 = "set":

			if get_all_npc() does not contain arg-2:
				send feedback("error", "nem létezik")
				stop

			if arg-3 = "type":
				set_type(arg-2, arg-4)
				send feedback("success", "%arg-2% type %arg-4%")

			if arg-3 = "name":
				set_name(arg-2, arg-4)
				send feedback("success", "%arg-2% nev %arg-4%")

		if arg-1 = "list":

			send feedback("casual", join get_all_npc())

		if arg-1 = "spawn":

			if arg-2 is not set:
				send feedback("error", "adj nevet")
				stop

			if get_all_npc() does not contain arg-2:
				send feedback("error", "nem létezik")
				stop

			spawn_npc(location of player, arg-2)
			send feedback("success", "%arg-2% létrehozva")

		if arg-1 = "kill":

			kill_npc(target entity of player)
			send feedback("success", "eltüntetve")

on tab complete of "/npc":
	set tab completions for position 1 to "create", "delete", "set", "list", "spawn", "kill"
	if tab arg-1 = "create":
		set tab completions for position 2 to "<NPC neve>"

	if tab arg-1 = "delete":
		set tab completions for position 2 to get_all_npc()

	if tab arg-1 = "set":
		set tab completions for position 2 to get_all_npc()
		set tab completions for position 3 to "type", "name"

		if tab arg-3 = "type":
			set tab completions for position 4 to all entity types
			
		if tab arg-3 = "name":
			set tab completions for position 4 to "<NPC neve>"

	if tab arg-1 = "spawn":
		set tab completions for position 2 to get_all_npc()