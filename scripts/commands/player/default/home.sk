command /home [<text>]:

	prefix: kockacraft
	executable by: players
	usage: &f/home &a<otthon neve>
	description: Elteleportálás az otthonodhoz
	trigger:

		if arg-1 is not set:
			set {_homes} to (join indices of {home::%uuid of player%::*} with ", " ? "&c-")
			set {_current_home_count} to size of {home::%uuid of player%::*}
			set {_max_homes} to get_maximum_homes(player)

			send feedback("casual", "&fOtthonjaid &7(%{_current_home_count}%/%{_max_homes}%)&f: ß%{_homes}%")
			stop

		if {home::%uuid of player%::%arg-1%} is not set:
			send feedback("error", "&cNincs otthonod &4%arg-1% &cnévvel!")
			stop

		teleport player to {home::%uuid of player%::%arg-1%}
		send feedback("casual", "&fTeleportálás a(z) ß%arg-1% &fotthonodhoz...")
			
on tab complete of "/home":
	set tab completions for position 1 to indices of {home::%uuid of player%::*}