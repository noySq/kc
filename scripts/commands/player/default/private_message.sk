options:
	no-message: "&cNem adtál meg üzenetet!"
	target-offline: "&4%get_displayname({_target})% &cnem elérhető!"

	sending-format: "ßTE &f→ ß%get_displayname({_target})%&8: &r%colored {_message}%"
	receive-format: "%get_displayname({_p})% &f→ ßTE&8: &r%colored {_message}%"

command /message [<offlineplayer>] [<text>]:

	prefix: kockacraft
	aliases: m, msg, tell, whisper, w
	executable by: console and players
	usage: &f/m <játékos> <üzenet>
	description: Privát üzenet küldése egy játékosnak
	trigger:
		private_message(player, arg-1, arg-2)

command /reply [<text>]:

	prefix: kockacraft
	aliases: r
	executable by: players
	usage: &f/r <üzenet>
	description: Gyors válaszolás a privát üzenetre
	trigger:
		private_message(player, {-private_message::%uuid of player%::reply}, arg-1)

on tab complete of "/message", "/m", "/msg", "/tell", "/whisper", "/w":
	set tab completions for position 1 to all players
	set tab completions for position 2 to "<üzenet>"

local function private_message(p: player, target: offlineplayer, message: text):

	if {_message} is not set:
		send feedback("error", {@no-message}) to {_p}
		stop
	
	if is_player_online({_target}) = false:
		send feedback("error", {@target-offline}) to {_p}
		stop
		
	send feedback("casual", {@sending-format}) to {_p}

	set {_message} to feedback("casual", {@receive-format})
	send_player_message({_target}, {_message})

	set {-private_message::%uuid of {_target}%::reply} to {_p}
	set {-private_message::%uuid of {_p}%::reply} to {_target}